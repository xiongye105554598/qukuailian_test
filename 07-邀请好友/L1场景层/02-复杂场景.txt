*** Settings ***
Resource          ../L2接口层/邀请好友.txt
Resource          ../L2接口层/余额.txt
Resource          ../../通用/L2接口层/计划.txt
Resource          ../L3DB层/campaign_inviter.txt
Resource          ../../资源/清理数据.txt

*** Test Cases ***
在A的链接中注册，在B的链接中加入
    [Documentation]    【前置条件】
    ...
    ...    1）13410000610、13410000612账号未邀请过用户
    ...
    ...    2）13410000611是未注册用户
    ...
    ...    【步骤】
    ...
    ...    1）邀请者A 13410000610登录，进入邀请好友页
    ...
    ...    2）被邀请者B 13410000611通过A邀请好友的链接登录
    ...
    ...    3）B注册后，校验A和B的绑定关系
    ...
    ...    4）邀请者C 13410000612登录，进入邀请好友页
    ...
    ...    5）B通过C的链接登录后，B添加保障人并加入计划
    ...
    ...    6）B加入计划后，校验A和B的绑定关系各字段
    ...
    ...    7）校验A中账户余额的变化，邀请后余额=邀请前余额+邀请个数*邀请奖励，邀请个数仍然为1
    ...
    ...    8）校验C中账户余额的变化，邀请后余额=邀请前余额
    ...
    ...    9）校验A中邀请详情页的展示
    [Tags]    wsong
    [Setup]
    #全局参数
    ${计划类型}    set variable    zjwy_product
    ${邀请者A手机号}    set variable    23810000610
    ${邀请者A验证码}    根据手机号生成验证码    ${邀请者A手机号}
    ${邀请者C手机号}    set variable    23810000612
    ${邀请者C验证码}    根据手机号生成验证码    ${邀请者C手机号}
    ${被邀请者B手机号}    set variable    23810000611
    ${被邀请者B验证码}    根据手机号生成验证码    ${被邀请者B手机号}
    ${邀请者A邀请前余额}    查询账户余额    ${邀请者A手机号}
    ${邀请者C邀请前余额}    查询账户余额    ${邀请者C手机号}
    #邀请者A获取Cookie    #被邀请者B登录
    获取登录态    ${邀请者A手机号}    ${邀请者A验证码}
    ${guest_id}    ${PHPSESSID}    ${raw_utm_source}    ${utm_source}    进入邀请好友页    ${邀请者A手机号}
    被邀请者登录    ${被邀请者B手机号}    ${被邀请者B验证码}    ${guest_id}    ${PHPSESSID}    ${raw_utm_source}    ${utm_source}
    #校验B注册，A和B的关系
    校验注册后的绑定关系    ${邀请者A手机号}    ${被邀请者B手机号}
    #邀请者C获取Cookie    #被邀请者B登录
    获取登录态    ${邀请者C手机号}    ${邀请者C验证码}
    ${guest_id}    ${PHPSESSID}    ${raw_utm_source}    ${utm_source}    进入邀请好友页    ${邀请者C手机号}
    被邀请者登录    ${被邀请者B手机号}    ${被邀请者B验证码}    ${guest_id}    ${PHPSESSID}    ${raw_utm_source}    ${utm_source}
    #被邀请者B加入
    获取登录态    ${被邀请者B手机号}    ${被邀请者B验证码}
    单个保障人微信加入计划    在C链接中加入    110101198810205416    ${计划类型}    ${被邀请者B手机号}
    #校验B加入，A和B的关系
    校验加入计划后的绑定关系    ${邀请者A手机号}    ${被邀请者B手机号}    ${计划类型}
    #校验账户余额的变化
    ${邀请者A邀请后余额}    查询账户余额    ${邀请者A手机号}
    校验邀请好友后的余额变化    ${邀请者A邀请后余额}    ${邀请者A邀请前余额}    1
    ${邀请者C邀请后余额}    查询账户余额    ${邀请者C手机号}
    Should Be Equal As Strings    ${邀请者C邀请后余额}    ${邀请者C邀请前余额}
    #校验邀请者A我的邀请
    获取登录态    ${邀请者A手机号}    ${邀请者A验证码}
    查看并校验邀请详情页内容    ${被邀请者B手机号}    已成功
    [Teardown]    清理邀请关系    ${邀请者A手机号}    0    ${被邀请者B手机号}
