*** Settings ***
Resource          ../L2接口层/支付.txt
Resource          ../../资源/清理数据.txt
Resource          ../../资源/Http请求.txt
Resource          ../L2接口层/计划.txt
Resource          ../../通用/L2接口层/登录.txt
Resource          ../../通用/L2接口层/保障人.txt
Resource          ../../通用/L2接口层/交易记录.txt
Resource          ../L2接口层/交易记录.txt
Resource          ../L3DB层/hold.txt
Resource          ../L3DB层/order.txt
Resource          ../../通用/L3DB层/insured.txt
Resource          ../L2接口层/保障人.txt
Resource          ../../通用/L2接口层/实名认证.txt

*** Test Cases ***
选择已有保障人加入重疾无忧
    [Documentation]    【前置条件】
    ...
    ...    1）23810000211账号A初始余额0元
    ...
    ...    2）保障人110101198810204755之前未加入重疾无忧
    ...
    ...    【步骤】
    ...
    ...    1）账号A使用验证码登录，获取登录Cookie
    ...
    ...    2）保障人姓名：保障人已存在 身份证号：110101198810204755，在账号A用户我的保障人中添加该保障人
    ...
    ...    3）使用微信支付将2）中添加的保障人加入重疾无忧互助计划
    ...
    ...    4）校验Db信息，包含持有信息（hold表）、交易记录（order表）、账户余额（account_phone表）
    [Tags]    wsong
    [Setup]
    #登录获取Cookie    #手机号    #验证码
    ${用户手机号}    set variable    23810000211
    设置账户初始余额    ${用户手机号}    0
    自动生成验证码登录    ${用户手机号}
    #我的保障人中添加保障人
    ${计划类型}    set variable    zjwy_product
    ${保障人姓名}    set variable    选择已有保障人
    ${保障人身份证号}    set variable    110101198810204755
    ${保障人Id}    我的保障人添加保障人成功    ${保障人姓名}    ${保障人身份证号}    北京阿博茨科技有限公司    IT业    测试
    ...    测试工程师    test@niub.la
    #微信付费加入重疾无忧
    微信支付    ${计划类型}    ${保障人Id}
    #校验DB信息
    加入成功校验持有计划信息    ${用户手机号}    ${保障人Id}    ${计划类型}
    校验微信加入计划的交易记录    ${用户手机号}    ${计划类型}
    校验账户余额    ${用户手机号}    0
    [Teardown]    清理已加入的用户    ${用户手机号}    110101198810204755    0

保障人已存在，加入计划时继续添加该保障人
    [Documentation]    【注意点】
    ...
    ...    1）保障人已存在，加入计划时计划详情页添加该保障人，且保障人的信息与之前一致 此种场景仍然会调用添加保障人接口，只是Server不做处理
    ...
    ...    2）选择已有保障人加入重疾无忧 此种场景根本不会调用添加保障人接口
    ...
    ...    【前置条件】
    ...
    ...    1）23810000210账号A初始余额0元
    ...
    ...    2）保障人110101198810206013之前未加入重疾无忧
    ...
    ...    【步骤】
    ...
    ...    1）账号A使用验证码登录，获取登录Cookie
    ...
    ...    2）保障人姓名：保障人已存在 身份证号：110101198810206013，在账号A用户我的保障人中添加该保障人
    ...
    ...    3）在计划详情中再次添加该保障人
    ...
    ...    4）使用微信支付将2）中添加的保障人加入重疾无忧互助计划
    ...
    ...    5）校验Db信息，包含持有信息（hold表）、交易记录（order表）、账户余额（account_phone表）
    [Tags]    wsong
    [Setup]
    #登录获取Cookie    #手机号    #验证码
    ${用户手机号}    set variable    23810000210
    设置账户初始余额    ${用户手机号}    0
    自动生成验证码登录    ${用户手机号}
    #我的保障人中添加保障人
    ${计划类型}    set variable    zjwy_product
    ${保障人姓名}    set variable    保障人已存在
    ${保障人身份证号}    set variable    110101198810206013
    我的保障人添加保障人成功    ${保障人姓名}    ${保障人身份证号}    北京阿博茨科技有限公司    IT业    测试    测试工程师
    ...    test@niub.la
    #计划详情添加该保障人
    ${保障人Id}    计划详情添加保障人成功    ${计划类型}    ${保障人姓名}    ${保障人身份证号}
    #微信付费加入重疾无忧
    微信支付    ${计划类型}    ${保障人Id}
    #校验DB信息
    加入成功校验持有计划信息    ${用户手机号}    ${保障人Id}    ${计划类型}
    校验微信加入计划的交易记录    ${用户手机号}    ${计划类型}
    校验账户余额    ${用户手机号}    0
    [Teardown]    清理已加入的用户    ${用户手机号}    110101198810206013    0

保障人已加入重疾无忧，再加入重疾无忧
    [Documentation]    【前置条件】
    ...
    ...    1）23810000212账号A初始余额9元
    ...
    ...    2）保障人110101198810207534之前未加入重疾无忧
    ...
    ...    【步骤】
    ...
    ...    1）账号A使用验证码登录，获取登录Cookie
    ...
    ...    2）保障人姓名：重复加入重疾无忧 身份证号：110101198810207534，账号A用户添加该保障人
    ...
    ...    3）使用余额支付将2）中添加的保障人加入重疾无忧互助计划
    ...
    ...    4）校验Db信息，包含持有信息（hold表）、交易记录（order表）、账户余额（account_phone表）
    ...
    ...    5）使用微信支付将2）中添加的保障人再次加入重疾无忧互助计划，校验返回的错误信息
    [Tags]    wsong
    [Setup]
    #登录获取Cookie    #手机号    #验证码
    ${用户手机号}    set variable    23810000212
    设置账户初始余额    ${用户手机号}    900
    自动生成验证码登录    ${用户手机号}
    #获取保障人Id
    ${计划类型}    set variable    zjwy_product
    ${保障人姓名}    set variable    重复加入重疾无忧
    ${保障人身份证号}    set variable    110101198810207534
    ${保障人Id}    计划详情添加保障人成功    ${计划类型}    ${保障人姓名}    ${保障人身份证号}
    #余额加入重疾无忧
    余额支付    ${计划类型}    ${保障人Id}
    #校验DB信息
    加入成功校验持有计划信息    ${用户手机号}    ${保障人Id}    ${计划类型}
    校验余额加入计划的交易记录    ${用户手机号}    ${计划类型}
    校验账户余额    ${用户手机号}    0
    #微信付费加入重疾无忧
    不满足加入条件微信支付    ${计划类型}    ${保障人Id}    "保障人已经加入该计划"
    [Teardown]    清理已加入的用户    ${用户手机号}    110101198810207534    900

保障人已加入重疾无忧，再加入IT安康
    [Documentation]    【前置条件】
    ...
    ...    1）23810000213账号A初始余额0元
    ...
    ...    2）保障人110101198810207710之前未加入IT安康
    ...
    ...    【步骤】
    ...
    ...    1）账号A使用验证码登录，获取登录Cookie
    ...
    ...    2）保障人姓名：微信加入重疾无忧 身份证号：110101198810207710，账号A用户添加该保障人
    ...
    ...    3）使用微信支付将2）中添加的保障人加入重疾无忧互助计划
    ...
    ...    4）校验Db信息，包含持有信息（hold表）、交易记录（order表）、账户余额（account_phone表）
    ...
    ...    5）查询该用户我的计划，返回我的计划信息
    ...
    ...    6）校验5）中返回的我的计划信息中，是否包含该保障人姓名、身份证号和保障金9.00元
    ...
    ...    7）查询交易记录，返回我的交易记录
    ...
    ...    8）校验7）中返回的交易记录信息
    [Tags]    wsong
    [Setup]
    #登录获取Cookie    #手机号    #验证码
    ${用户手机号}    set variable    23810000213
    设置账户初始余额    ${用户手机号}    0
    自动生成验证码登录    ${用户手机号}
    #获取保障人Id
    ${计划类型}    set variable    zjwy_product
    ${保障人姓名}    set variable    已加入重疾再加入IT
    ${保障人身份证号}    set variable    110101198810209871
    ${保障人Id}    计划详情添加保障人成功    ${计划类型}    ${保障人姓名}    ${保障人身份证号}
    #微信付费加入重疾无忧
    微信支付    ${计划类型}    ${保障人Id}
    #校验DB信息
    加入成功校验持有计划信息    ${用户手机号}    ${保障人Id}    ${计划类型}
    校验微信加入计划的交易记录    ${用户手机号}    ${计划类型}
    校验账户余额    ${用户手机号}    0
    #微信付费加入IT安康
    ${新计划类型}    set variable    itak_product
    微信支付    ${新计划类型}    ${保障人Id}
    #校验DB信息
    加入多个计划校验持有计划信息    ${用户手机号}    ${新计划类型}    ${计划类型}
    校验多条微信加入计划的交易记录    ${用户手机号}    ${新计划类型}    ${计划类型}
    校验账户余额    ${用户手机号}    0
    [Teardown]    清理已加入的用户    ${用户手机号}    110101198810209871    0

实名认证用户加入重疾无忧
    [Documentation]    【前置条件】
    ...
    ...    1）23810000229账号A初始余额0元
    ...
    ...    2）保障人110101198810207358之前未加入重疾无忧
    ...
    ...    【步骤】
    ...
    ...    1）账号A使用验证码登录，获取登录Cookie
    ...
    ...    2）用户姓名：实名认证姓名 身份证号：110101198810207358， 进行实名认证操作
    ...
    ...    3）该用户微信付费加入重疾无忧
    [Tags]    wsong
    [Setup]
    #变量
    ${用户手机号}    set variable    23810000229
    设置账户初始余额    ${用户手机号}    0
    ${用户验证码}    set variable    9020
    ${真实姓名}    set variable    实名认证姓名
    ${真实身份证号}    set variable    110101198810207358
    ${计划类型}    set variable    zjwy_product
    #登录
    获取登录态    ${用户手机号}    ${用户验证码}
    #实名认证
    实名认证    ${真实姓名}    ${真实身份证号}    ${用户验证码}    success    true
    #微信付费加入重疾无忧
    单个保障人微信加入计划    ${真实姓名}    ${真实身份证号}    ${计划类型}    ${用户手机号}
    [Teardown]    清理实名信息和加入计划的用户    ${用户手机号}    110101198810207358    0

保障人退出计划后再加入计划
    [Documentation]    【前置条件】
    ...
    ...    1）23810000230账号A初始余额0元
    ...
    ...    2）保障人110101198810202653之前未加入重疾无忧
    ...
    ...    【步骤】
    ...
    ...    1）账号A使用验证码登录，获取登录Cookie
    ...
    ...    2）用户添加保障人并加入计划，保障人姓名：退出计划后再加入 身份证号：110101198810202653
    ...
    ...    3）用户让该保障人退出计划
    ...
    ...    4）校验账户余额：9元
    ...
    ...    5）再次将该保障人加入计划
    ...
    ...    6）校验DB信息（hold表）和账户余额
    [Tags]    wsong
    [Setup]
    #变量
    ${用户手机号}    set variable    23810000230
    ${保障人姓名}    set variable    退出计划后再加入
    ${保障人身份证号}    set variable    110101198810202653
    ${计划类型}    set variable    zjwy_product
    #登录
    自动生成验证码登录    ${用户手机号}
    设置账户初始余额    ${用户手机号}    0
    #保障人加入计划
    单个保障人微信加入计划    ${保障人姓名}    ${保障人身份证号}    ${计划类型}    ${用户手机号}
    ${保障人Id}    根据身份证号查保障人ID    ${保障人身份证号}
    #保障人退出计划
    多个保障人退出计划    ${用户手机号}    ${计划类型}    1
    校验账户余额    ${用户手机号}    900
    #该保障人再次加入计划
    自动生成验证码登录    ${用户手机号}
    余额支付    ${计划类型}    ${保障人Id}
    #校验DB信息
    退出再加入成功校验持有计划信息    ${用户手机号}    ${保障人Id}    ${计划类型}
    校验账户余额    ${用户手机号}    0
    [Teardown]    清理已加入的用户    ${用户手机号}    110101198810202653    0

未添加保障人加入重疾无忧
    [Documentation]    【前置条件】
    ...
    ...    1）23810000231账号A初始余额0元
    ...
    ...    【步骤】
    ...
    ...    1）账号A使用验证码登录，获取登录Cookie
    ...
    ...    2）不添加保障人，直接使用微信支付，校验错误信息（请选择保障人）
    ...
    ...    4）校验Db信息，包含持有信息（hold表）、交易记录（order表）、账户余额（account_phone表）
    [Tags]    wsong
    [Setup]
    #参数
    ${用户手机号}    set variable    23810000231
    ${计划类型}    set variable    zjwy_product
    #登录
    自动生成验证码登录    ${用户手机号}
    设置账户初始余额    ${用户手机号}    0
    #微信付费加入重疾无忧
    不满足加入条件微信支付    ${计划类型}    0    "请选择保障人"
    #校验DB信息
    校验账户余额    ${用户手机号}    0
    加入失败校验持有计划信息    ${用户手机号}    0    ${计划类型}
    校验加入失败的交易记录    ${用户手机号}    ${计划类型}
    [Teardown]    设置账户初始余额    ${用户手机号}    0

使用别人的保障人加入重疾无忧
    [Documentation]    【前置条件】
    ...
    ...    1）用户A13410000232，用户B13410000233均未注册，用户B初始余额为0
    ...
    ...    【步骤】
    ...
    ...    1）用户A 13410000232使用验证码登录，获取登录Cookie
    ...
    ...    2）保障人姓名：使用别人的保障人加入重疾 身份证号：110101198810204771，在用户A我的保障人中添加该保障人
    ...
    ...    3）用户B 13410000233用户登录
    ...
    ...    4）用户B使用用户A刚添加的保障人加入计划，校验错误信息
    ...
    ...    5）校验Db信息，包含持有信息（hold表）、交易记录（order表）
    [Tags]    wsong
    [Setup]
    #变量
    ${用户A手机号}    set variable    23810000232
    ${用户B手机号}    set variable    23810000233
    #用户A登录
    自动生成验证码登录    ${用户A手机号}
    #我的保障人中添加保障人
    ${计划类型}    set variable    zjwy_product
    ${保障人姓名}    set variable    使用别人的保障人加入重疾
    ${保障人身份证号}    set variable    110101198810204771
    ${保障人Id}    我的保障人添加保障人成功    ${保障人姓名}    ${保障人身份证号}    北京阿博茨科技有限公司    IT业    测试
    ...    测试工程师    test@niub.la
    #用户B登录
    自动生成验证码登录    ${用户B手机号}
    设置账户初始余额    ${用户B手机号}    0
    #微信付费加入重疾无忧
    不满足加入条件微信支付    ${计划类型}    ${保障人Id}    "请选择保障人"
    #校验DB信息
    加入失败校验持有计划信息    ${用户B手机号}    0    ${计划类型}
    校验加入失败的交易记录    ${用户B手机号}    ${计划类型}
    [Teardown]    清理保障人信息    ${用户A手机号}    110101198810204771
