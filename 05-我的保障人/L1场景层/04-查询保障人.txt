*** Settings ***
Resource          ../../通用/L2接口层/登录.txt
Resource          ../L2接口层/查询保障人列表.txt
Resource          ../L2接口层/删除保障人.txt
Resource          ../../通用/L2接口层/保障人.txt
Resource          ../../资源/清理数据.txt
Resource          ../L2接口层/查询某保障人.txt

*** Test Cases ***
我的保障人中有0个保障人
    [Documentation]    【前置条件】
    ...
    ...    1）13410000311账号未添加任何保障人
    ...
    ...    【步骤】
    ...
    ...    1）13410000311获取登录Cookie
    ...
    ...    2）查询保障人列表，校验返回的列表是否为空
    [Tags]    wsong
    #登录获取Cookie    #手机号    #验证码
    ${用户手机号}    set variable    23810000311
    自动生成验证码登录    ${用户手机号}
    #查询保障人列表
    ${保障人信息}    查询保障人列表
    校验保障人列表为空    ${保障人信息}

我的保障人中有2个保障人
    [Documentation]    【前置条件】
    ...
    ...    1）13410000312账号未添加任何保障人
    ...
    ...    【步骤】
    ...
    ...    1）13410000312获取登录Cookie
    ...
    ...    2）添加保障人A，保障人姓名：保障人A，保障人身份证号：110101198810208422
    ...
    ...    3）休眠1s（以防两个添加的记录created_at一致，导致查询出的记录顺序不正确）
    ...
    ...    4）添加保障人B，保障人姓名：保障人B，保障人身份证号：110101198810209724
    ...
    ...    5）查看保障人列表
    ...
    ...    6）校验列表返回的保障人信息中，个数、保障人ID是否正确
    [Tags]    wsong
    [Setup]
    #登录
    ${用户手机号}    set variable    23810000312
    自动生成验证码登录    ${用户手机号}
    #添加保障人A
    ${保障人A姓名}    set variable    保障人A
    ${保障人A身份证号}    set variable    110101198810208422
    ${保障人AId}    添加保障人时无职业    ${保障人A姓名}    ${保障人A身份证号}
    Sleep    1s
    #添加保障人B
    ${保障人B姓名}    set variable    保障人B
    ${保障人B身份证号}    set variable    110101198810209724
    ${保障人BId}    添加保障人时无职业    ${保障人B姓名}    ${保障人B身份证号}
    #查询保障人列表
    ${保障人信息}    查询保障人列表
    校验保障人列表信息    ${保障人信息}    2    ${保障人BId}    ${保障人AId}
    [Teardown]    清理保障人信息    ${用户手机号}    110101198810208422    110101198810209724
