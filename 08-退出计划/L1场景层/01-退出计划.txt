*** Settings ***
Resource          ../../通用/L2接口层/登录.txt
Resource          ../../02-加入计划/L2接口层/保障人.txt
Resource          ../../02-加入计划/L2接口层/支付.txt
Resource          ../L2接口层/控制台.txt
Resource          ../L3DB层/hold.txt
Resource          ../../资源/清理数据.txt
Resource          ../L3DB层/order.txt
Resource          ../../通用/L3DB层/hold.txt

*** Test Cases ***
加入计划后，从控制台退出计划
    [Documentation]    【前置条件】
    ...
    ...    1）13410000400账号未添加保障人，账户余额1000000000元
    ...
    ...    【步骤】
    ...
    ...    1）1341000400登录
    ...
    ...    2）1）中用户添加保障人并加入计划
    ...
    ...    3）校验账户余额
    ...
    ...    4）从控制台将该用户退出计划
    ...
    ...    5）查询hold表中is_out字段的值是否为1（表示已退出）
    ...
    ...    6）校验账户余额
    [Tags]    wsong
    [Setup]
    #登录
    ${用户手机号}    set variable    23810000400
    设置账户初始余额    ${用户手机号}    1000000000
    自动生成验证码登录    ${用户手机号}
    #添加保障人加入计划
    ${计划类型}    set variable    zjwy_product
    ${保障人Id}    计划详情添加保障人成功    ${计划类型}    加入计划后退出计划    11010119881020355X
    余额支付    ${计划类型}    ${保障人Id}
    ${账户余额}    Evaluate    1000000000-900
    校验账户余额    ${用户手机号}    ${账户余额}
    #从控制台退出计划
    @{holdIdList}    查询某用户加入某计划的holdIdList    ${用户手机号}    ${计划类型}    1
    ${hold_id}    set variable    ${holdIdList[0]}
    : FOR    ${hold_id}    IN    @{holdIdList}
    \    控制台退出计划    ${hold_id}    自动化测试
    #校验退出计划后的DB信息
    校验退出计划后的持有信息    ${hold_id}
    #退出计划后，校验保障金
    校验账户余额    ${用户手机号}    1000000000
    #退出计划后，校验交易记录
    校验退出计划的交易记录    ${用户手机号}    ${计划类型}    900
    [Teardown]    清理加入和退出计划的记录    ${用户手机号}    11010119881020355X    1000000000

退出计划后，补贴金额不会退到余额
    [Documentation]    【前置条件】
    ...
    ...    1）13410000401账号之前已领取赠送（gift_id：2342），该赠送赠送800分
    ...
    ...    【步骤】
    ...
    ...    1）1341000401登录
    ...
    ...    2）退出已领取的计划
    ...
    ...    3）校验账户余额是否为900-800=100
    ...
    ...    4）校验退出计划后的交易记录
    [Tags]    wsong
    [Setup]
    #登录
    ${用户手机号}    set variable    23810000401
    设置账户初始余额    ${用户手机号}    0
    自动生成验证码登录    ${用户手机号}
    #用户退出计划
    ${计划类型}    set variable    zjwy_product
    @{holdIdList}    查询某用户加入某计划的holdIdList    ${用户手机号}    ${计划类型}    1
    控制台退出计划    ${holdIdList[0]}    自动化测试    #只领取了一个赠送计划
    #校验账户余额
    校验账户余额    ${用户手机号}    100    #赠送800，用户自费100    #退出时退回100
    #退出计划后，校验交易记录
    校验退出计划的交易记录    ${用户手机号}    ${计划类型}    100
    [Teardown]    恢复退出计划前数据    ${用户手机号}    0

退出计划后，校验退出记录
    [Documentation]    【前置条件】
    ...
    ...    1）13410000402账号未添加保障人
    ...
    ...    【步骤】
    ...
    ...    1）13410000402登录
    ...
    ...    2）1）中用户添加保障人并加入计划
    ...
    ...    3）从控制台将该用户退出计划
    ...
    ...    4）查询控制台的退出记录
    ...
    ...    5）校验退出记录中是否包含该用户、该保障人
    [Tags]    wsong
    [Setup]
    #变量
    ${用户手机号}    set variable    23810000402
    设置账户初始余额    ${用户手机号}    0
    ${保障人姓名}    set variable    校验退出记录
    ${保障人身份证号}    set variable    110101198810203218
    ${计划类型}    set variable    zjwy_product
    #登录
    自动生成验证码登录    ${用户手机号}
    #加入计划
    单个保障人微信加入计划    ${保障人姓名}    ${保障人身份证号}    ${计划类型}    ${用户手机号}
    #退出计划
    多个保障人退出计划    ${用户手机号}    ${计划类型}    1
    #校验退出记录
    控制台查看退出记录
    校验ResponseBody是否包含    ${用户手机号}
    校验ResponseBody是否包含    ${保障人姓名}
    校验ResponseBody是否包含    ${保障人身份证号}
    [Teardown]    清理加入和退出计划的记录    ${用户手机号}    110101198810203218    0
