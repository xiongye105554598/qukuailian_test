*** Settings ***
Resource          ../../通用/L2接口层/计划.txt
Resource          ../../资源/清理数据.txt
Resource          ../../通用/L3DB层/plan.txt

*** Test Cases ***
筹备期计划有3人时加入1人，计划状态不变
    [Documentation]    【前置条件】
    ...
    ...    1）13410000420账号未添加保障人，zjwy_product-1420计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-1420，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）1341000020登录
    ...
    ...    3）2）中用户添加3个保障人并加入计划
    ...
    ...    4）计划状态此时为1（筹备期）
    ...
    ...    5）1个保障人加入计划
    ...
    ...    6）校验计划状态是否仍然为1（筹备期）
    [Tags]    wsong
    [Setup]
    #添加重疾无忧互助计划
    ${计划类型}    set variable    zjwy_product-1420
    添加重疾无忧互助计划    ${计划类型}
    #登录
    ${用户手机号}    set variable    23810000420
    设置账户初始余额    ${用户手机号}    1000000000
    自动生成验证码登录    ${用户手机号}
    #3个保障人加入计划
    ${保障人信息}    create dictionary    保一=110101198810201677    保二=110101198810207139    保三=110101198810202354
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    校验计划状态    ${计划类型}    1    #筹备期
    #1个保障人加入计划
    ${保障人信息}    create dictionary    增一=110101198810203912
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    #校验计划状态
    校验计划状态    ${计划类型}    1    #筹备期
    [Teardown]    清理添加的计划和用户    zjwy_product-1420    ${用户手机号}    1000000000

筹备期超过180天未达到5人，计划解散
    [Documentation]    【前置条件】
    ...
    ...    1）13410000422账号未添加保障人，zjwy_product-1422计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-1422，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）1341000022登录
    ...
    ...    3）2）中用户添加1个保障人并加入计划
    ...
    ...    4）计划状态此时为1（筹备期）
    ...
    ...    5）修改计划的state_update_time为180天前
    ...
    ...    6）2）中用户再添加1个保障人并加入计划
    ...
    ...    7）计划状态此时为5（已解散）
    [Tags]    wsong
    [Setup]
    #添加重疾无忧互助计划
    ${计划类型}    set variable    zjwy_product-1422
    添加重疾无忧互助计划    ${计划类型}
    #登录
    ${用户手机号}    set variable    23810000422
    设置账户初始余额    ${用户手机号}    1800
    自动生成验证码登录    ${用户手机号}
    #1个保障人加入计划
    ${保障人信息}    create dictionary    保一=110101198810201677
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    校验计划状态    ${计划类型}    1    #筹备期
    #修改日期
    修改计划状态更新时间    ${计划类型}    15552000
    #1个保障人加入计划
    ${保障人Id}    计划详情添加保障人成功    ${计划类型}    增一    110101198810203912
    余额支付    ${计划类型}    ${保障人Id}
    #校验计划状态
    校验计划状态    ${计划类型}    5    #已解散
    #校验账户余额
    校验账户余额    ${用户手机号}    1800    #加入的保障人的钱退回到余额
    [Teardown]    清理添加的计划和用户    zjwy_product-1422    ${用户手机号}    1800

成长期计划有7人时加入1人，计划状态不变
    [Documentation]    【前置条件】
    ...
    ...    1）13410000421账号未添加保障人，zjwy_product-1421计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-1421，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）1341000421登录
    ...
    ...    3）2）中用户添加7个保障人并加入计划
    ...
    ...    4）计划状态此时为2（成长期）
    ...
    ...    5）1个保障人加入计划
    ...
    ...    6）校验计划状态是否仍然为2（成长期）
    [Tags]    wsong
    [Setup]
    #添加重疾无忧互助计划
    ${计划类型}    set variable    zjwy_product-1421
    添加重疾无忧互助计划    ${计划类型}
    #登录
    ${用户手机号}    set variable    23810000421
    设置账户初始余额    ${用户手机号}    1000000000
    自动生成验证码登录    ${用户手机号}
    #7个保障人加入计划
    ${保障人信息}    create dictionary    保一=110101198810201677    保二=110101198810207139    保三=110101198810202354    保四=11010119881020523X    保五=110101198810208393
    ...    保六=110101198810201992    保七=110101198810209134
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    校验计划状态    ${计划类型}    2    #成长期
    #1个保障人加入计划
    ${保障人信息}    create dictionary    增一=110101198810203912
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    #校验计划状态
    校验计划状态    ${计划类型}    2    #成长期
    [Teardown]    清理添加的计划和用户    zjwy_product-1421    ${用户手机号}    1000000000

休眠期计划有3人时加入1人，计划状态不变
    [Documentation]    【前置条件】
    ...
    ...    1）13410000416账号未添加保障人，zjwy_product-1416计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-1416，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）1341000016登录
    ...
    ...    3）2）中用户添加9个保障人并加入计划，6个保障人退出该计划，计划变为休眠期
    ...
    ...    4）1个人再加入计划
    ...
    ...    5）校验计划状态是否仍然为4（休眠期）
    [Tags]    wsong
    [Setup]
    #添加重疾无忧互助计划
    ${计划类型}    set variable    zjwy_product-1416
    添加重疾无忧互助计划    ${计划类型}
    #登录
    ${用户手机号}    set variable    23810000416
    设置账户初始余额    ${用户手机号}    1000000000
    自动生成验证码登录    ${用户手机号}
    #9个加入计划，6个退出
    ${保障人信息}    create dictionary    保一=110101198810201677    保二=110101198810207139    保三=110101198810202354    保四=11010119881020523X    保五=110101198810208393
    ...    保六=110101198810201992    保七=110101198810209134    保八=11010119881020435X    保九=110101198810202338
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    多个保障人退出计划    ${用户手机号}    ${计划类型}    6
    #校验计划状态
    校验计划状态    ${计划类型}    4    #休眠期
    #1个人再加入计划
    自动生成验证码登录    ${用户手机号}    #如果这里不获取登录态    #会使用控制台账户的登陆态
    ${保障人信息}    create dictionary    增一=110101198810203912
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    #校验计划状态
    校验计划状态    ${计划类型}    4    #休眠期
    [Teardown]    清理添加的计划和用户    zjwy_product-1416    ${用户手机号}    1000000000

休眠期计划有3人时加入3人，计划变成成长期
    [Documentation]    【前置条件】
    ...
    ...    1）13410000417账号未添加保障人，zjwy_product-1417计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-1417，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）1341000017登录
    ...
    ...    3）2）中用户添加9个保障人并加入计划，6个保障人退出该计划，计划变为休眠期
    ...
    ...    4）3个人再加入计划
    ...
    ...    5）校验计划状态是否为2（成长期）
    [Tags]    wsong
    [Setup]
    #添加重疾无忧互助计划
    ${用户手机号}    set variable    23810000417
    ${计划类型}    set variable    zjwy_product-1417
    添加重疾无忧互助计划    ${计划类型}
    #登录
    设置账户初始余额    ${用户手机号}    1000000000
    自动生成验证码登录    ${用户手机号}
    #9个加入计划，6个退出
    ${保障人信息}    create dictionary    保一=110101198810201677    保二=110101198810207139    保三=110101198810202354    保四=11010119881020523X    保五=110101198810208393
    ...    保六=110101198810201992    保七=110101198810209134    保八=11010119881020435X    保九=110101198810202338
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    多个保障人退出计划    ${用户手机号}    ${计划类型}    6
    #校验计划状态
    校验计划状态    ${计划类型}    4    #休眠期
    #3个人再加入计划
    自动生成验证码登录    ${用户手机号}    #如果这里不获取登录态    #会使用控制台账户的登陆态
    ${保障人信息}    create dictionary    增一=110101198810203912    增二=110101198810207390    增三=110101198810202477
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    #校验计划状态
    校验计划状态    ${计划类型}    2    #成长期
    [Teardown]    清理添加的计划和用户    zjwy_product-1417    ${用户手机号}    1000000000

休眠期超过30天未达到6人，计划解散
    [Documentation]    【前置条件】
    ...
    ...    1）13410000423账号未添加保障人，zjwy_product-1423计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-1423，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）1341000023登录
    ...
    ...    3）2）中用户添加6个保障人加入计划，4个保障人退出该计划，计划变为休眠期
    ...
    ...    4）修改计划状态更新时间，修改为30天前
    ...
    ...    5）1个用户加入计划
    ...
    ...    6）校验计划状态，是否变为已解散
    ...
    ...    7）校验账户余额
    [Tags]    wsong
    [Setup]
    #变量
    ${计划类型}    set variable    zjwy_product-1423
    ${用户手机号}    set variable    23810000423
    #添加重疾无忧互助计划
    添加重疾无忧互助计划    ${计划类型}
    #登录
    设置账户初始余额    ${用户手机号}    5400
    自动生成验证码登录    ${用户手机号}
    #6个保障人加入计划
    ${保障人信息}    create dictionary    保一=110101198810201677    保二=110101198810207139    保三=110101198810202354    保四=11010119881020523X    保五=110101198810208393
    ...    保六=110101198810201992
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    校验计划状态    ${计划类型}    2    #成长期
    校验账户余额    ${用户手机号}    0
    #4个保障人退出计划
    多个保障人退出计划    ${用户手机号}    ${计划类型}    4
    校验账户余额    ${用户手机号}    3600
    #校验计划状态
    校验计划状态    ${计划类型}    4    #休眠期
    #修改日期
    修改计划状态更新时间    ${计划类型}    2592000
    #1个保障人加入计划
    自动生成验证码登录    ${用户手机号}
    ${保障人Id}    计划详情添加保障人成功    ${计划类型}    增一    110101198810203912
    余额支付    ${计划类型}    ${保障人Id}
    #校验计划状态
    校验计划状态    ${计划类型}    5    #已解散
    #校验账户余额
    校验账户余额    ${用户手机号}    5400    #加入的保障人的钱退回到余额
    [Teardown]    清理添加的计划和用户    zjwy_product-1423    ${用户手机号}    5400
