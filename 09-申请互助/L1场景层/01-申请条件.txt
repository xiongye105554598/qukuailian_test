*** Settings ***
Resource          ../../通用/L2接口层/计划.txt
Resource          ../../通用/L3DB层/plan.txt
Resource          ../../通用/L2接口层/保障人.txt
Resource          ../L2接口层/申请互助.txt
Resource          ../../资源/清理数据.txt
Resource          ../L3DB层/hold.txt
Resource          ../L3DB层/claim_task.txt
Resource          ../../08-退出计划/L2接口层/控制台.txt
Resource          ../L3DB层/insured.txt
Resource          ../../通用/L2接口层/计划.txt
Resource          ../../通用/L2接口层/计划.txt

*** Test Cases ***
用户观察期不可申请互助
    [Documentation]    【前置条件】
    ...
    ...    1）13410000500账号初始余额45元，zjwy_product-0500计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-0500，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）13410000500登录
    ...
    ...    3）2）中用户添加5个保障人并加入计划
    ...
    ...    4）计划状态此时为2（成长期）
    ...
    ...    5）调用查询保障人信息接口，查询保障人holdId
    ...
    ...    6）调用申请互助接口申请互助，提示信息：用户仍然在观察期内,无法提起互助申请
    [Tags]    wsong
    [Setup]
    #添加重疾无忧计划
    ${计划类型}    set variable    zjwy_product-0500
    添加重疾无忧互助计划    ${计划类型}
    #用户登录
    ${用户手机号}    set variable    23810000500
    设置账户初始余额    ${用户手机号}    4500
    自动生成验证码登录    ${用户手机号}
    #5个保障人加入计划
    ${保障人信息}    create dictionary    保一=110101198810201677    保二=110101198810207139    保三=110101198810202354    保四=11010119881020523X    保五=110101198810208393
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    校验计划状态    ${计划类型}    2    #成长期
    #查询保障人信息，申请互助
    ${持有计划的HoldId}    ${持有计划的保障项目}    保障人信息查询    110101198810201677    保一
    申请互助请求    ${持有计划的HoldId}    ${持有计划的保障项目}    用户仍然在观察期内,无法提起互助申请
    [Teardown]    清理添加的计划和用户    zjwy_product-0500    ${用户手机号}    4500

计划筹备期不可申请互助
    [Documentation]    【前置条件】
    ...
    ...    1）13410000501账号初始余额0元，zjwy_product-0501计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-0501，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）13410000501登录
    ...
    ...    3）添加保障人加入该计划，保障人姓名：筹备期申请互助，保障人身份证号：110101198810201677
    ...
    ...    4）计划状态此时为1（筹备期）
    ...
    ...    5）调用查询保障人信息接口，查询保障人holdId
    ...
    ...    6）修改用户加入计划时间，使用户处于保障期
    ...
    ...    7）调用申请互助接口申请互助，提示信息：当前产品状态不允许提出互助申请
    [Tags]    wsong
    [Setup]
    #添加重疾无忧计划
    ${计划类型}    set variable    zjwy_product-0501
    添加重疾无忧互助计划    ${计划类型}
    #用户登录
    ${用户手机号}    set variable    23810000501
    设置账户初始余额    ${用户手机号}    900
    自动生成验证码登录    ${用户手机号}
    #添加保障人加入计划
    ${保障人姓名}    set variable    筹备期申请互助
    ${保障人身份证号}    set variable    110101198810201677
    ${保障人信息}    create dictionary    ${保障人姓名}=${保障人身份证号}
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    校验计划状态    ${计划类型}    1    #筹备期
    #查询保障人信息，申请互助
    ${持有计划的HoldId}    ${持有计划的保障项目}    保障人信息查询    ${保障人身份证号}    ${保障人姓名}
    修改加入计划时间    ${持有计划的HoldId}
    申请互助请求    ${持有计划的HoldId}    ${持有计划的保障项目}    当前产品状态不允许提出互助申请
    [Teardown]    清理添加的计划和用户    zjwy_product-0501    ${用户手机号}    900

计划成长期可申请互助
    [Documentation]    【前置条件】
    ...
    ...    1）13410000502账号初始余额45元，zjwy_product-0502计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-0502，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）13410000500登录
    ...
    ...    3）2）中用户添加5个保障人并加入计划
    ...
    ...    4）计划状态此时为2（成长期）
    ...
    ...    5）调用查询保障人信息接口，查询保障人holdId
    ...
    ...    6）修改用户加入计划时间，使用户处于保障期
    ...
    ...    7）调用申请互助接口申请互助，提示信息：互助操作任务创建成功,请等待后台任务执行!
    ...
    ...    8）校验Db信息：claim_task中是否有此holdId的任务
    [Tags]    wsong
    [Setup]
    #添加重疾无忧计划
    ${计划类型}    set variable    zjwy_product-0502
    添加重疾无忧互助计划    ${计划类型}
    #用户登录
    ${用户手机号}    set variable    23810000502
    设置账户初始余额    ${用户手机号}    4500
    自动生成验证码登录    ${用户手机号}
    #5个保障人加入计划
    ${保障人信息}    create dictionary    保一=110101198810201677    保二=110101198810207139    保三=110101198810202354    保四=11010119881020523X    保五=110101198810208393
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    校验计划状态    ${计划类型}    2    #成长期
    #查询保障人信息，申请互助
    ${持有计划的HoldId}    ${持有计划的保障项目}    保障人信息查询    110101198810201677    保一
    修改加入计划时间    ${持有计划的HoldId}
    申请互助请求    ${持有计划的HoldId}    ${持有计划的保障项目}    互助操作任务创建成功,请等待后台任务执行!
    #校验Db信息
    校验申请互助任务    ${持有计划的HoldId}
    [Teardown]    清理添加的计划和用户    zjwy_product-0502    ${用户手机号}    4500

计划运行期可申请互助
    [Documentation]    【前置条件】
    ...
    ...    1）13410000503账号初始余额81元，zjwy_product-0503计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-0503，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）13410000503登录
    ...
    ...    3）2）中用户添加9个保障人并加入计划
    ...
    ...    4）计划状态此时为3（运行期）
    ...
    ...    5）调用查询保障人信息接口，查询保障人holdId
    ...
    ...    6）修改用户加入计划时间，使用户处于保障期
    ...
    ...    7）调用申请互助接口申请互助，提示信息：互助操作任务创建成功,请等待后台任务执行!
    ...
    ...    8）校验Db信息：claim_task中是否有此holdId的任务
    [Tags]    wsong
    [Setup]
    #添加重疾无忧计划
    ${用户手机号}    set variable    23810000503
    ${计划类型}    set variable    zjwy_product-0503
    添加重疾无忧互助计划    ${计划类型}
    #用户登录
    设置账户初始余额    ${用户手机号}    8100
    自动生成验证码登录    ${用户手机号}
    #9个保障人加入计划
    ${保障人信息}    create dictionary    保一=11010119881020507X    保二=110101198810207139    保三=110101198810202354    保四=11010119881020523X    保五=110101198810208393
    ...    保六=110101198810201992    保七=110101198810209134    保八=11010119881020435X    保九=110101198810202338
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    校验计划状态    ${计划类型}    3    #运行期
    #查询保障人信息，申请互助
    ${持有计划的HoldId}    ${持有计划的保障项目}    保障人信息查询    11010119881020507X    保一
    修改加入计划时间    ${持有计划的HoldId}
    申请互助请求    ${持有计划的HoldId}    ${持有计划的保障项目}    互助操作任务创建成功,请等待后台任务执行!
    #校验Db信息
    校验申请互助任务    ${持有计划的HoldId}
    [Teardown]    清理添加的计划和用户    zjwy_product-0503    ${用户手机号}    8100

计划休眠期不可申请互助
    [Documentation]    【前置条件】
    ...
    ...    1）13410000504账号初始余额81元，zjwy_product-0504计划不存在
    ...
    ...    【步骤】
    ...
    ...    1）添加重疾无忧互助计划，计划id：zjwy_product-0504，该计划配置：成长期（6）、运行期（10）、休眠期（4）
    ...
    ...    2）13410000504登录
    ...
    ...    3）2）中用户添加9个保障人并加入计划
    ...
    ...    4）再将6个保障人退出计划
    ...
    ...    5）此时计划状态为4（休眠期）
    ...
    ...    6）查询仍在计划中的某用户
    ...
    ...    7）调用查询保障人信息接口，查询保障人holdId
    ...
    ...    8）修改用户加入计划时间，使用户处于保障期
    ...
    ...    9）调用申请互助接口申请互助，提示信息：当前产品状态不允许提出互助申请
    [Tags]    wsong
    [Setup]
    #添加重疾无忧计划
    ${用户手机号}    set variable    23810000504
    ${计划类型}    set variable    zjwy_product-0504
    添加重疾无忧互助计划    ${计划类型}
    #用户登录
    设置账户初始余额    ${用户手机号}    8100
    自动生成验证码登录    ${用户手机号}
    #9个保障人加入计划
    ${保障人信息}    create dictionary    保一=110101198810201677    保二=110101198810207139    保三=110101198810202354    保四=11010119881020523X    保五=110101198810208393
    ...    保六=110101198810201992    保七=110101198810209134    保八=11010119881020435X    保九=110101198810202338
    多个保障人余额加入计划    ${计划类型}    ${用户手机号}    ${保障人信息}
    #6个保障人退出计划
    多个保障人退出计划    ${用户手机号}    ${计划类型}    6
    校验计划状态    ${计划类型}    4    #休眠期
    #查询仍在计划中的用户
    ${保障人姓名}    ${保障人身份证号}    查询未退出计划的用户    ${用户手机号}
    #查询保障人信息，申请互助
    ${持有计划的HoldId}    ${持有计划的保障项目}    保障人信息查询    ${保障人身份证号}    ${保障人姓名}
    修改加入计划时间    ${持有计划的HoldId}
    申请互助请求    ${持有计划的HoldId}    ${持有计划的保障项目}    当前产品状态不允许提出互助申请
    [Teardown]    清理添加的计划和用户    zjwy_product-0504    ${用户手机号}    8100
